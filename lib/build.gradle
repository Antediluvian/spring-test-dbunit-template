apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    //persistence
    compile 'com.github.springtestdbunit:spring-test-dbunit:1.2.1'
    compile 'org.dbunit:dbunit:2.4.9'
    compile 'org.springframework:spring-test:4.1.6.RELEASE'

    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

jar {
    archiveName "${rootProject.name}-${rootProject.version}.jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveName "${rootProject.name}-${rootProject.version}-javadoc.jar"
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveName "${rootProject.name}-${rootProject.version}-sources.jar"
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def publishStage() {
    project.hasProperty('publishStage')? publishStage: false
}

if (publishStage()) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    groupId rootProject.group
                    artifactId rootProject.name
                    version rootProject.version

                    name rootProject.name
                    packaging 'jar'
                    description 'An extension to simplify CRUD test with spring-test-dbunit'
                    url 'https://github.com/Hippoom/spring-test-dbunit-template'

                    scm {
                        url 'scm:git@github.com:Hippoom/spring-test-dbunit-template.git'
                        connection 'scm:git@github.com:Hippoom/spring-test-dbunit-template.git'
                        developerConnection 'scm:git@github.com:Hippoom/spring-test-dbunit-template.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://github.com/Hippoom/spring-test-dbunit-template/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'hippoom'
                            name 'Yugang Zhou'
                        }
                    }
                }
            }
        }
    }
}